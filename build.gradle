plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

group = 'com.github.takemikami'
version = '0.0.3'

def digdagVersion = '0.10.0'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/digdag/maven'
    }
}

configurations {
    provided
}

dependencies {
    provided 'io.digdag:digdag-spi:' + digdagVersion
    provided 'io.digdag:digdag-plugin-utils:' + digdagVersion
    testCompile 'junit:junit:4.+'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // artifactId 'project1-sample'        
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

checkstyle {
    toolVersion '8.40'
}

pmd {
    toolVersion '6.31.0'
    sourceSets = [sourceSets.main]
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/multithreading.xml",
            "category/java/security.xml"
    ]
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}